---
title: xv6 Chapter 3 - Turn on Paging
date: 2019-01-29
draft: false
---

In the last post, we discussed about the C code part of the boot loader, namely `bootmain.c`. It read 4096 bytes data from the beginning of the disk second sector into the physical address `0x10000` then parsed the elf file header by a `struct elf`, read specified fields of `struct elf` in order to validate elf file and acquire information about the program table. After that our boot loader fetched every corresponding elf body segment (e.g. .text .data) defined by each entry in the program header table and placed them in the written physical address `p_addr` By the end of boot loader it called `p->entry` which is the entry of the elf file, proceeding to the next program, which is `main`.

<!--more-->

The most difficult part of the boot loader is knowing how to parse the elf file structure. I assumed the reader was familiar with the virtual memory layout for a typical executable program, including what `.text` `.data` `.bss` etc. are and where they locate at. If you cannot really catch that please refer to *CSAPP* or other books. Back to the topic, an elf contains useful information that is necessary for an elf loader so our boot loader has actually performed a simple loading process in few lines.

Here to remind you of the action sequence of the boot, we did not directly come from the very beginning to our boot loader program, because the machine need a tiny program (512 bytes), which was automatically imported from first sector on the disk since powered-up, that fact means the BIOS probably has the similar boot loader like bootmain.c, however this sneaky boot loader is stored in the mother-board’s rom other than disks and we cannot touch this pre-prepared boot loader. 

When the BIOS boot loader finished its duty, it gave the power to our 512-byte boot program, our tiny boot program contains code that tweaked our cpu settings and the boot loader. In a word the solidified BIOS boot loader loaded our boot program, there are two boot programs in different places and work in sequence. Make sure you are clear before continue.

# Abstract

In this chapter, the kernel has already been in the memory, but where is it? The kernel has been compiled and linked so that it expects to find itself at virtual addresses starting at `0x80100000`. Thus, function call instructions must mention destination addresses that look like `0x801xxxxx`; you can see examples in `kernel.asm`. This address is configured in `kernel.ld` (9311). `0x80100000` is a relatively high address, towards the end of the 32-bit address space;  further chapters will explain the reasons for this choice. There may not be any physical memory at such a high address. Once the kernel starts executing, it will set up the paging hardware to map virtual addresses starting at `0x80100000` to physical addresses starting at `0x00100000`; the kernel assumes that there is physical memory at this lower address. At this point in the boot process, however, paging is not enabled. Instead, kernel.ld specifies that the ELF p_addr start at `0x00100000`, which causes the boot loader to copy the kernel to the low physical addresses to which the paging hardware will eventually point.

That is quite a big load of words, simply speaking:

* The kernel’s entry is placed at physical address `0x00100000`, because address `0xa00000` to `0x100000` is reserved by IO devices. (Plus we assigned address `0x0` to `0x7c00` used for stack in `bootasm.S`)
* The kernel entry cannot be called directly since paging hardware is not enabled yet, and kernel uses virtual address for its functions. You can enter the kernel by `jmp` instruction but once we were in the kernel, all functions, variables cannot be found.
* **Conclusion**: We need a transition program to set up the virtual map for the kernel. It is linked with the `main.c` program, and where `p->entry` fell.

# Turn on paging

We need to do 3 things to turn on paging:

1. Set the size for the virtual memory page, which also regarded as page size. By convention, it should be either 4K bytes or 4M bytes. Here we chose 4M bytes.
2. We need to create the page table entries (PTE) for every page, currently we only need the one for `0x80100000` (We must sure that the elf is smaller than 4M) and 0x0 the address where the `%eip` is pointing at now. With 4M page, that number is 512 and 0 respectively.
3. Set the entry for page table directory in `%cr3`.
3. Turn on paging, by setting a specified bit in `%cr0`.

The kernel’s physical address (`p_addr`) is used for our boot loader, it tells to allocate the first program segment at physical address `0x10000`. However, from the kernel’s perspective, it assumes itself locates at `v_addr` `0x80100000`. So we need to cast the virtual address `0x80000000` to the physical address `0x0`, thus entry 512 has the base address `0x0`.

Here is a simple illustration of how mmu interprets virtual address:

1. Read `v_addr`.
2. Calculate the index of PTE implicitly, because we chose 4M page size so the value of the segment register is `v_addr/4M`.
3. Fetch page table entry 512. The address was calculated by step 2.
4. Check whether the page is missing, and validate the operation (i.e writing to a read-only page is illegal).
5. Add up offset and the base address of the page, return the result physical address to cpu. (In some processor like later the Core i7, there are several levels of PTE so we need more steps to get the physical address.)

In the transition program, we also created the entry 0 for our current program, it points to `0x0`, where we are now and the same address as entry 512 does, otherwise the program will crash after we turn on the paging function in `%cr0`, because cpu cannot find the next instruction.

When we finished, we needed an indirect jump (fill the `%eip` by hand), since the assembler would otherwise generate the PC-related version of jump, and the result address is a low address, which is invalid in virtual address system.

# Code notes

`entry.S` was linked with `main.c`, so the elf contained them both. Let's first read the header notes of `entry.S`:

`entry.S:1-3`:

```x86asm
# The xv6 kernel starts executing in this file. This file is linked with
# the kernel C code, so it can refer to kernel symbols such as main().
# The boot block (bootasm.S and bootmain.c) jumps to entry below.
```

See? And there is the `_start` convention: `entry.S:37-41`
```x86asm
# By convention, the _start symbol specifies the ELF entry point.
# Since we havent set up virtual memory yet, our entry point is
# the physical address of entry.
.globl _start
_start = V2P_WO(entry)
```

The `_start` labeled the first function of the elf when executed, because `entry.S` and `main.c` were linked together, They shared the same entry point. The linker we used assumed all the segments locate at virtual addresses starting from `0x80100000` which includes `entry`. That is why we need to use macro `V2P_WO()` to change `entry` address. Otherwise, in the last step: `entry()` cannot be executed, since the  function pointer `p->entry` points to a high virtual address, and at that time we have not turned on paging yet.

After called `p->entry`, we came here: `entry.S:43-56`

```x86asm
# Entering xv6 on boot processor, with paging off.
.globl entry
entry:
  # Turn on page size extension for 4Mbyte pages
  movl    %cr4, %eax
  orl     $(CR4_PSE), %eax
  movl    %eax, %cr4
  # Set page directory
  movl    $(V2P_WO(entrypgdir)), %eax
  movl    %eax, %cr3
  # Turn on paging.
  movl    %cr0, %eax
  orl     $(CR0_PG|CR0_WP), %eax
  movl    %eax, %cr0
```

A series of operations to turn on paging. Please note that `entrypgdir` must be physical address, it is required by MMU and the linker created the virtual version. Since different CPUs have different paging config registers, so I do not want to explain in details about `%cr0` and `%cr3` `%cr4` registers. When you really need to write a OS, you have to refer to the corresponding processor manual. Different generations of CPU differ from each other on paging registers.

However, please know the `entrypgdir` structure is defined in `main.c:97-108`

```c
// The boot page table used in entry.S and entryother.S.
// Page directories (and page tables) must start on page boundaries,
// hence the __aligned__ attribute.
// PTE_PS in a page directory entry enables 4Mbyte pages.

__attribute__((__aligned__(PGSIZE)))
pde_t entrypgdir[NPDENTRIES] = {
  // Map VA's [0, 4MB) to PA's [0, 4MB)
  [0] = (0) | PTE_P | PTE_W | PTE_PS,
  // Map VA's [KERNBASE, KERNBASE+4MB) to PA's [0, 4MB)
  [KERNBASE>>PDXSHIFT] = (0) | PTE_P | PTE_W | PTE_PS,
};
```

As I said above, the page table contains two entries, one for `0x0` and one for `0x80000000` and the page table is strictly aligned, it is a global variable, so it can be found in the elf's `.data` segment.

Next, prepare for calling `main()`: `entry.S:58-68`

```x86asm
  # Set up the stack pointer.
  movl $(stack + KSTACKSIZE), %esp

  # Jump to main(), and switch to executing at
  # high addresses. The indirect call is needed because
  # the assembler produces a PC-relative instruction
  # for a direct jump.
  mov $main, %eax
  jmp *%eax

.comm stack, KSTACKSIZE
```

When it comes to call a function, always remember to **make sure the stack is available now**, we have changed address configuration so we want to change stack pointer.

The final line has a strange command `.comm`. Here is its usage:

```x86asm
 .comm name, size, alignment
 # The .comm directive allocates storage in the data section. 
 # The storage is referenced by the identifier name. Size is 
 # measured in bytes and must be a positive integer. Name 
 # cannot be predefined. Alignment is optional. If alignment 
 # is specified, the address of name is aligned to a multiple 
 # of alignment.
```

`.comm stack, KSTACKSIZE` allocated a storage `stack` of size `KSTACKSIZE` in `.data` segment and can be linked by others (just like global variables), `KSTACKSIZE` are defined in `param.h:2`

```c
#define KSTACKSIZE 4096  // size of per-process kernel stack
```

Per-process kernel stack allows multiple processes trapped in the kernel at the same time. These stacks seem to be isolated, kernel knows which stack is for which process when switches to different processes.

In the end, we use an indirect jump to transfer the processor to `main` in `main.c`.

# Conclusion

In this chapter, we talked about the process of turning on paging in xv6, the code implementation can be found in entry.S file. The code is pretty simple but has a lot important things in how to handle details of the P2V transition including indirect jump, and the PTE 0.

In the next post we will discuss the details of the creating the first process in xv6.


