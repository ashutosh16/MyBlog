---
title: xv6 Chapter 1 - Bootstrap
date: 2019-01-26
draft: false
---

The xv6 note collection is based on the textbook and source code provided by MIT class code 6.828,which also called the *xv6 a Unix-like operating system*. All the materials including codes and scripts are under [the MIT License](https://mit-license.org/).

<!--more-->

This post is the first chapter in the collection, we will go through the basic knowledge of Intel 8088 processor and do the bootstrap work for xv6 kernel step by step.

Xv6 provided official textbook [xv6 book](https://pdos.csail.mit.edu/6.828/2018/xv6/book-rev11.pdf) online to illustrate what the codes are trying to do. The text version of code can be downloaded here [xv6 source](https://pdos.csail.mit.edu/6.828/2018/xv6/xv6-rev11.pdf). 

# After powering up

After you powered up your processor, a small program would automatically executed. The program was not designed by us, instead it was burned in the rom. And it performed a very simple action, transferring the data on first sector of disk to the main memory address `0x7c00` and jumped to that location. In order not to get confused about the boot loader in next chapter, we called the ex-factory boot loader *BIOS boot loader*.

After that, processor turned to our program, namely `bootasm.S`. All the operations will performed in `bootasm.S`:

* Disable the BIOS interrupts
* Clear all segment registers including DS, ES, SS except CS, it can only be modified by `ljmp` instruction.
* Open A20 Gate, make the 21st address line valid, or the 21st bit of address register will always be 0, the address is not consecutive.(It is also required by protected mode)
* Create GDT, here we choose GDT to be flat, the address is not changed and 1 to 1 mapped.
* Transform CPU from `Real Mode` to `Protected Mode`, enabling 32-bit addressing. 

We will go through them one by one in the following.

First we need to know what BIOS had done before our program `bootasm.S:3-6`：

```x86asm
# Start the first CPU: switch to 32-bit protected mode, jump into C.
# The BIOS loads this code from the first sector of the hard disk into
# memory at physical address 0x7c00 and starts executing in real mode
# with %cs=0 %ip=7c00.
```

We know that our program entry is at `0x7c00`, and `%cs` had been cleared . In the initial real mode, the CPU can only address in 16 bit mode.

## Disable BIOS interrupts

Let us focus on `bootasm.S:13`.

```x86asm
  cli                         # Disable interrupts
```

It is a convention for the programmer to disable BIOS interrupts, because in this period, the interrupt handlers are not available, so when an interrupt occurs, it will cause undefined behaviors.

Please be advised about `.code16`, it means until the next related symbols, the assembler will only generate 16-bit format machine code.

## Clear segment registers

Go to `bootasm.S:15-19`

```x86asm
  # Zero data segment registers DS, ES, and SS.
  xorw    %ax,%ax             # Set %ax to zero
  movw    %ax,%ds             # -> Data Segment
  movw    %ax,%es             # -> Extra Segment
  movw    %ax,%ss             # -> Stack Segment
```

These segment registers are all of 16-bit width, besides there is another segment register named `CS`, it cannot be modified by `mov` or other math and logic instructions. The default value for `CS` is zero, so there is no need to clear `CS` register. We will discuss `CS` later in the article.

The three registers contains different types of addresses and they are chosen implicitly by processor. For example, when the instruction being executed consists instruction address, like `jmp foo` processor will automatically fetch the value from `%cs` which is the address of `foo`. If the processor met `push %ax`, it will automatically choose `%ss`.

* `%ds` Data Segment Register.
* `%ss` Stack Segment Register.
* `%cs` Code Segment Register.
* `%es` Extra Segment Register. Only used for duplicating and depositing value from other segment register.

The reason why called segment register is that, Intel processors use a special method to address variables in which the corresponding segment register is shifted to left 4 bits (equals x16) then added with a 16-bit offset register. This method allows the processor to do a 20-bit addressing with two 16-bit registers, however, it also caused another problem, putting forward the origin of A20 Gate.

## Open the A20 Gate

As I said above, the final address that cpu fetches is calculated like this:

```c
final_address=(seg_reg<<4)+offset_reg;
```

What if we input `seg_reg=0xFFFF, offset_reg=0xFFFF`?

The result is `final_address=0x10FFEF` which actually is a 21-bit value. Back to the days when Intel 8086 only had 20 address lines, the 21st bit was ignored. 

But after that Intel released processor with more than 20 address lines, however, Intel still wanted to make them backward compatible, so the 21st address line was ignored by default. IBM used a small tricks to solve this problem, it assigned a unused pin named A20 in the 8042 controller (PS/2 controller) to be the switch of 21st address line, that was, the origin of A20 Gate.

What if we do not open the A20 Gate? Well, if we had an address like `0xFFFFFFFF` which is longer than 20 bits, the 21st bit would always be 0, thus the memory space is not consecutive, we are only accessible to 0~1MB, 2~3MB, 4~5MB memory addresses. That was a definitely catastrophy for programmer. I have not try it, who knows lol.

How to open A20 gate? The approach is simple, we just let the 2nd pin of 8042 controller output 1. But the operations are pretty complex, first we must know the mechanics of 8042 I/O.

The related instructions can be found here `bootasm.S:21-37`。

```x86asm
  # Physical address line A20 is tied to zero so that the first PCs 
  # with 2 MB would run software that assumed 1 MB.  Undo that.
seta20.1:
  inb     $0x64,%al               # Wait for not busy
  testb   $0x2,%al
  jnz     seta20.1

  movb    $0xd1,%al               # 0xd1 -> port 0x64
  outb    %al,$0x64

seta20.2:
  inb     $0x64,%al               # Wait for not busy
  testb   $0x2,%al
  jnz     seta20.2

  movb    $0xdf,%al               # 0xdf -> port 0x60
  outb    %al,$0x60
```

Intel CPU controls 8042 through I/O ports. As we know, cpu is not only made of cores, there are also many peripheral controllers on chip like ethernet, usb, PCI, etc. The processor addresses them from `0x0` to `0xFF`. As for 8042, its I/O number is `0x60` and `0x64`.


| IO Port | Access Type | Purpose  |
| :---: | :---: | :---: |
| 0x60 | R/W | Data Port |
| 0x64 | R | Status Register |
| 0x64 | W | Command Register |

`0x64` port connects to different registers for different access types, which is pretty common in embedded system.

The opening A20 gate contains following operations:

* Wait for not busy, by checking the status register. Loop while busy.
* Send command to 0x64, indicating an incoming output.
* Wait for not busy, by checking the status register. Loop while busy.
* Send data to 0x60, output 1 on 2nd pin.

Details on [8042 Controller](https://wiki.osdev.org/%228042%22_PS/2_Controller))

From now on, we can make 21-bit addressing (up to 2MB). It is slightly better than 20-bit version (up to 1MB) however we want 32-bit addressing (up to 4GB) in the end. 

## Turn on 32-bit addressing

By default, when CPU is powered up, it ran in Real Mode, where we can only do 21-bit address. We can change it into Protected Mode, in which 32-bit addressing is available.

But to transform cpu into Protected Mode, we must create a table called GDT. Every entry in GDT contains:

* a 32-bit base address field
* a 20-bit limit field
* a 12-bit flag field

What a GDT entry does? First, CPU no longer used segment registers to addressing anymore, instead, the segment registers are used as an index in GDT, each value from (0-65535) maps an entry in the table.

CPU will fetch the 32-bit base address in the entry and add it with another register named `%eip` which is familiar by most programmers. The result is a 32-bit address.

Another advantage of using GDT is the high security, GDT is considered as the cpu's hardware mechanics to isolate the executable code from data if we set the flags in the flag field, it labels what memory is executable and cannot be modified, and what memory (i.e data) cannot be executed. It prevent the overflow attack from a hardware level.

In x86 asm, GDT is loaded by `ldgt` instruction, and GDT is defined in  `bootasm.S:78-87`。

```x86asm
# Bootstrap GDT
.p2align 2                                # force 4 byte alignment
gdt:
  SEG_NULLASM                             # null seg
  SEG_ASM(STA_X|STA_R, 0x0, 0xffffffff)   # code seg
  SEG_ASM(STA_W, 0x0, 0xffffffff)         # data seg

gdtdesc:
  .word   (gdtdesc - gdt - 1)             # sizeof(gdt) - 1
  .long   gdt                             # address gdt
```

We fill three entries in GDT, they are:

* Null segment, this segment has no real function, but usually reserved for debug purpose.
* Code segment, labeled by the 12-bit flag. Base address is `0x00`, maximum offset is `0xffffffff`.
* Data segment, labeled by the 12-bit flag. Base address is `0x00`, maximum offset is `0xffffffff`.
* Per cpu data segment, I do not know nothing about it.

Want to know more about GDT, please refer to [Global Descriptor Table - OSDev Wiki](https://wiki.osdev.org/Global_Descriptor_Table).

In the end, move 1 to `%cr0` to open Protected Mode, we need to reload `%cs` and `%eip` registers. The only way to modify `%cs` is by `ljmp` to the other place, here we jumped to `start32`.

Next, reload DS, ES, SS. `bootasm.S:54-62`。

```x86asm
start32:
  # Set up the protected-mode data segment registers
  movw    $(SEG_KDATA<<3), %ax    # Our data segment selector
  movw    %ax, %ds                # -> DS: Data Segment
  movw    %ax, %es                # -> ES: Extra Segment
  movw    %ax, %ss                # -> SS: Stack Segment
  movw    $0, %ax                 # Zero segments not ready for use
  movw    %ax, %fs                # -> FS
  movw    %ax, %gs                # -> GS
```

Here we successfully bootstrap our xv6 system, including turning on A20 gate, open Protected Mode. After these, we need to transfer the processor to `bootmain.c` which is our boot loader program.

Before we proceed, we need to check the `%esp`, because we cannot have the stack pointer points to `0x0` before calling, the stack grow from up to down, `0x0` means everything pushed into the stack will be lost. Since our boot program starts at `0x7c00`, we assigned `%esp` to `0x7c00`, it is ok because we do not need a lot space on stack, and it is proper to use space from `0x0000` to `0x7c00`. `bootasm.S:64-66`

```x86asm
  # Set up the stack pointer and call into C.
  movl    $start, %esp
  call    bootmain
```

# Review

We discussed about the hardware preparation for xv6, including:

* Disable BIOS interrupts.
* Clear segment registers.
* Open A20 Gate.
* Create GDT.
* Transform CPU into Protected Mode

In the next chapter, we proceed to the boot loader program, and please know it is different from the BIOS one. 

